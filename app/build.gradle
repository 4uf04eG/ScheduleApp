apply plugin: 'com.android.application'

ext.versionMajor = 1
ext.versionMinor = 1
ext.versionPatch = 2
ext.versionClassifier = null
ext.minimumSdkVersion = 16

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.ilya.scheduleapp"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode generateVersionCode()
        versionName generateVersionName()
        resConfigs "en" ,"ru"
        vectorDrawables.useSupportLibrary = false
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            proguardFiles 'proguard-rules.pro', getDefaultProguardFile('proguard-android.txt')
            minifyEnabled = true
            debuggable = false
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources true
    }
    compileOptions {
        targetCompatibility = '1.8'
        sourceCompatibility = '1.8'
    }
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor *
            10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }

    return versionName
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.preference:preference:1.1.0-rc01'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.1'
    implementation 'androidx.work:work-runtime:2.1.0'
    implementation 'com.github.QuadFlask:colorpicker:0.0.13'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.powermock:powermock-core:2.0.2'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.2'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.3.0-alpha02'
}
